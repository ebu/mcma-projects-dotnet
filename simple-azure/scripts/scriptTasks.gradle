task setMcmaVersion {
    def scriptFiles = fileTree(".") {
        include "**/*.csx"
    }

    inputs.files(scriptFiles)
    inputs.property("VERSION", mcmaVersion)
    outputs.files(scriptFiles)

    doLast {
        scriptFiles.each {File scriptFile ->
            def scriptFileContents = scriptFile.getText("UTF-8")

            def updated =
                scriptFileContents.replaceAll(
                    /#r\s*"nuget:\s*(Mcma(?:\.\w+)+),\s*\d+\.\d+\.\d+(?:-(?:alpha|beta|rc)\d*)?\s*"/,
                    '#r "nuget:$1, ' + mcmaVersion + '"')

            scriptFile.write(updated)
        }
    }
}

task run(type: Exec) {
    dependsOn ":deployment:terraformOutput"

    commandLine dotnetExecutable
    args "script"
    args "main.csx"
    args "--"
    args "--azureTenantId=${azureTenantId}"
    args "--azureClientId=${azureClientId}"
    args "--azureClientSecret=${azureClientSecret}"
    args "--testFilePath=${testFilePath}"
}